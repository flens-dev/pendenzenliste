buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
}
plugins {
    id "com.github.node-gradle.node" version "3.5.1"
    id 'org.springframework.boot' version '3.0.0'
    id 'java'
    id 'com.vaadin' version "${vaadinVersion}"
}

node {
    download = true
    version = "16.19.0"
    pnpmVersion = "7.23.0"
}

defaultTasks("clean", "build")

dependencies {
    implementation project(':boundary')

    runtimeOnly project(':boundary-usecases')
    runtimeOnly project(':gateway-filesystem')

    implementation enforcedPlatform("com.vaadin:vaadin-bom:${vaadinVersion}")

    implementation("com.vaadin:vaadin-core")
    implementation("com.vaadin:vaadin-spring-boot-starter")

    // We need to use tomcat version 9.x because vaadin only supports servlet-api 4.x
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:9.0.70'
    implementation "javax.servlet:javax.servlet-api:4.0.1"
}

vaadinPrepareFrontend.dependsOn(pnpmInstall)

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
    }
}

vaadin {
    pnpmEnable = true
}
