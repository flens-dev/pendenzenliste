buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.spotbugs.snom:spotbugs-gradle-plugin:5.1.4"
    }
}

configure(subprojects.findAll()) {
    group 'pendenzenliste'
    version '1.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'idea'

    apply plugin: 'jacoco'
    apply plugin: 'pmd'
    apply plugin: 'com.github.spotbugs'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'jakarta.inject:jakarta.inject-api:2.0.1'

        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter:${junitJupiterVersion}"
        testImplementation "org.junit.platform:junit-platform-suite:${junitPlatformVersion}"

        testImplementation "io.cucumber:cucumber-java:${cucumberVersion}"
        testImplementation "io.cucumber:cucumber-junit-platform-engine:${cucumberVersion}"

        testImplementation "org.assertj:assertj-core:${assertJVersion}"
        testImplementation "org.mockito:mockito-core:${mockitoVersion}"

        testImplementation "com.tngtech.archunit:archunit-junit5:${archUnitVersion}"

        compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugsAnnotationVersion}"
        spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:${findsecbugsVersion}"
    }

    test {
        useJUnitPlatform()
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
            vendor.set(JvmVendorSpec.ADOPTIUM)
        }
    }

    tasks.test {
        finalizedBy(tasks.jacocoTestReport)
    }
    tasks.jacocoTestReport {
        dependsOn(tasks.test)
    }

    jacocoTestReport {
        reports {
            xml.required = false
            csv.required = false
        }
    }

    pmd {
        consoleOutput = false
        sourceSets = [project.sourceSets.main]
        toolVersion = "6.55.0"
        incrementalAnalysis = true

        //TODO: Set this flag to false as soon as the issues are fixed
        ignoreFailures = true
    }

    project.spotbugs {
        toolVersion = '4.8.0'

        //TODO: Set this flag to false as soon as the issues are fixed
        ignoreFailures = true
    }

    tasks.matching { it.name.contains("spotbugs") }.each {
        if (it.name.contains("Test")) {
            it.enabled = false
        }

        it.reports {
            xml.enabled = false
            html.enabled = true
        }
    }
}